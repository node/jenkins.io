---
layout: documentation
title: Running multiple steps
section: doc
---

:toc:

Pipeline由多个步骤组成，支持编译、测试及部署应用程序。Jenkins Pipeline能很方便地创建多个步骤，这有助于建模各种类型的自动化流程。

一个“步骤”可以认为就像一个单一指令一样，用来执行单一操作。当一个步骤完成后，就继续下一个步骤；当一个步骤执行失败后，Pipeline也将失败。

当Pipeline中的所有步骤都成功完成时，就认为Pipeline也成功执行了。

=== Linux、BSD 和 Mac OS

在Linux、BSD 和 Mac OS（类Unix）系统上，`sh` 步骤用于在Pipeline里执行shell命令。

[pipeline]
----
// Declarative //
pipeline {
    agent any
    stages {
        stage('Build') {
            steps {
                sh 'echo "Hello World"'
                sh '''
                    echo "Multiline shell steps works too"
                    ls -lah
                '''
            }
        }
    }
}
// Scripted //
node {
    stage('Build') {
        sh 'echo "Hello World"'
        sh '''
            echo "Multiline shell steps works too"
            ls -lah
        '''
    }
}
----

=== Windows系统

Windows系统将使用 `bat` 步骤来执行批处理命令。

[pipeline]
----
// Declarative //
pipeline {
    agent any
    stages {
        stage('Build') {
            steps {
                bat 'set'
            }
        }
    }
}
// Scripted //
node {
    stage('Build') {
        bat 'set'
    }
}
----


== 超时、重试和其他

一些功能强大的步骤，会通过封装其他步骤来更容易地解决一些问题，比如重试（`retry`）直到成功，或者如果耗时太长就退出（`timeout`）。

[pipeline]
----
// Declarative //
pipeline {
    agent any
    stages {
        stage('Deploy') {
            steps {
                retry(3) {
                    sh './flakey-deploy.sh'
                }

                timeout(time: 3, unit: 'MINUTES') {
                    sh './health-check.sh'
                }
            }
        }
    }
}
// Scripted //
node {
    stage('Deploy') {
        retry(3) {
            sh './flakey-deploy.sh'
        }

        timeout(time: 3, unit: 'MINUTES') {
            sh './health-check.sh'
        }
    }
}
----

"Deploy" 阶段会重试 `flakey-deploy.sh` 脚本3次，然后最多等待3分钟来执行 `health-check.sh` 脚本。如果健康检查脚本没有在3分钟内完成，Pipeline将标记为在 "Deploy" 阶段失败。

像  `timeout` 和 `retry` 之类的 "Wrapper" 步骤可能会包含其他步骤，包括 `timeout` 或 `retry` 。

可以把这些步骤组合到一起，比如，如果想重试部署5次，但在该阶段失败前累计耗时不超过3分钟。

[pipeline]
----
// Declarative //
pipeline {
    agent any
    stages {
        stage('Deploy') {
            steps {
                timeout(time: 3, unit: 'MINUTES') {
                    retry(5) {
                        sh './flakey-deploy.sh'
                    }
                }
            }
        }
    }
}
// Scripted //
node {
    stage('Deploy') {
        timeout(time: 3, unit: 'MINUTES') {
            retry(5) {
                sh './flakey-deploy.sh'
            }
        }
    }
}
----

== 完成

当Pipeline执行完成后，需要执行清理步骤，或者针对Pipeline结果执行一些操作，这些操作可以放在 `post` 环节执行：

[pipeline]
----
// Declarative //
pipeline {
    agent any
    stages {
        stage('Test') {
            steps {
                sh 'echo "Fail!"; exit 1'
            }
        }
    }
    post {
        always {
            echo 'This will always run'
        }
        success {
            echo 'This will run only if successful'
        }
        failure {
            echo 'This will run only if failed'
        }
        unstable {
            echo 'This will run only if the run was marked as unstable'
        }
        changed {
            echo 'This will run only if the state of the Pipeline has changed'
            echo 'For example, if the Pipeline was previously failing but is now successful'
        }
    }
}
// Scripted //
node {
    try {
        stage('Test') {
            sh 'echo "Fail!"; exit 1'
        }
        echo 'This will run only if successful'
    } catch (e) {
        echo 'This will run only if failed'

        // Since we're catching the exception in order to report on it,
        // we need to re-throw it, to ensure that the build is marked as failed
        throw e
    } finally {
        def currentResult = currentBuild.result ?: 'SUCCESS'
        if (currentResult == 'UNSTABLE') {
            echo 'This will run only if the run was marked as unstable'
        }

        def previousResult = currentBuild.previousBuild?.result
        if (previousResult != null && previousResult != currentResult) {
            echo 'This will run only if the state of the Pipeline has changed'
            echo 'For example, if the Pipeline was previously failing but is now successful'
        }

        echo 'This will always run'
    }
}
----

---

在完成定义多步骤的基础后，请 **link:../agents[继续 "Defining execution environments"]**

include::doc/_feedback-footer.adoc[]
